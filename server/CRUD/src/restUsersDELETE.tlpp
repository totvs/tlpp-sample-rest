#include "tlpp-core.th"
#include "tlpp-rest.th"

/*------------------------------------------------------------------
deleta um usuário
Parâmetros via [raw body]
------------------------------------------------------------------*/

@Delete("sample/crud/delete/body/user")
user function deleteBodyUser()
    local cUser := ''
    local jBody := ''

    jBody := JsonObject():new()
    jBody:fromJson( oRest:GetBodyRequest() )

    if ( jBody <> Nil )
        cUser   := jBody:GetJsonText("user")
    endif
return processDeleteUser(cUser)

/*----------------------------------------------------------------
deleta um usuário
Parâmetros via [querystring]
----------------------------------------------------------------*/
@Delete("sample/crud/delete/query/user")
user function deleteQueryUser()
    local cUser     := ''
    local jQuery    := jsonObject():New() 
    
    jQuery := oRest:GetQueryRequest()
    if ( jQuery <> Nil )
        cUser  := jQuery['user']
    endif
return processDeleteUser(cUser)

/*
------------------------------------------------------------------
deleta um usuário
Parâmetros via [pathstring]
------------------------------------------------------------------
*/
@Delete("sample/crud/delete/path/user/:user")
user function deletePathUser()
    local cUser := ''
    local jPath := jsonObject():New()

    jPath := oRest:GetPathParamsRequest()

    if ( jPath <> Nil )
        cUser   := jPath[ 'user' ]
    endif
return processDeleteUser(cUser)

// ------------------------------------------------------------------ //
// ------------------------------------------------------------------ //
static function processDeleteUser(cUser)

    local cReturn   := ''
    local lOK       := .T.
    local cMsg      := ''

    // Valida campo chave de busca
        if ( cUser == Nil .or. empty(cUser) )
            cUser   := ''
            lOK     := .F.
            cMsg    := 'invalid parameter: user'
        endif

    if ( lOK )
        if ( !(lOK := sampleCRUDtlppRestdeleteUser( cUser )) )
            cMsg := 'could not deleted'
        else
            cMsg := 'record deleted'
        endif
    endif

    cReturn := '{ "SUCESS": "' + if(lOK,'yes','no') + '", "MESSAGE": "' + cMsg + '" }'

return oRest:SetResponse( cReturn )